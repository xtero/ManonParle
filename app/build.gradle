apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "28.0.1"
    defaultConfig {
        applicationId 'org.eu.nveo.manonparle'
        minSdkVersion 21
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
                includeCompileClasspath = true
            }
        }
        targetSdkVersion 26
        versionCode 10
        versionName '0.8.0'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            buildConfigField('String', 'DEFAULT_REPO', '"http://10.0.2.2/"')
        }
        release {
            buildConfigField('String', 'DEFAULT_REPO', '"http://manonparlerepo.nveo.eu.org/"')
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        staging {
            buildConfigField('String', 'DEFAULT_REPO', '"http://manonparlerepo.nveo.eu.org/"')
            debuggable true
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled false
            zipAlignEnabled true
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName = "${archivesBaseName}-${variant.name}-${variant.versionName}.apk"
                }
            }
        }
    }
    productFlavors {
    }
    aaptOptions {
        noCompress '.json', '.zip'
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def roomVersion = '1.1.1'
    def ioVersion = '2.6'
    implementation 'com.squareup.okhttp3:okhttp:4.0.1'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.github.duanhong169:colorpicker:1.1.2'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "commons-io:commons-io:$ioVersion"
    implementation "android.arch.persistence.room:runtime:$roomVersion"
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support.test:runner:1.0.2'
    implementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test:testing-support-lib:0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation "android.arch.persistence.room:testing:$roomVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$roomVersion"
    implementation 'com.android.support:gridlayout-v7:27.1.1'
}

project.afterEvaluate {
    preBuild.dependsOn generateZipAsset
}


task generateZipAsset(type: Zip) {
    from "res/"
    include "definition.json"
    include "medias/*"
    archiveName "base.zip"
    destinationDir(file("src/main/assets"))
}
